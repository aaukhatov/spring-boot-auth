buildscript {
	ext {
		springBootVersion = '2.3.3.RELEASE'
	}
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'com.bmuschko:gradle-docker-plugin:6.7.0'
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

project.version = "0.2.0"
sourceCompatibility = '11'
targetCompatibility = '11'

//bootJar {
//	baseName = 'auth-server'
//	version =  '0.1.0'
//	mainClassName = 'com.aukhatov.auth.AuthServerMain'
//}

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.security.oauth:spring-security-oauth2:2.3.4.RELEASE')
	implementation('org.springframework.security:spring-security-jwt:1.0.9.RELEASE')
	implementation('io.springfox:springfox-swagger2:2.7.0')
	implementation('io.springfox:springfox-swagger-ui:2.7.0')
	implementation('com.sun.xml.bind:jaxb-core:2.3.0.1')
	implementation('javax.xml.bind:jaxb-api:2.3.1')
	implementation('com.sun.xml.bind:jaxb-impl:2.3.1')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.security:spring-security-test')
}

task copyJar(type: Copy) {
	dependsOn   bootJar
	from        "build/libs/${jar.baseName}-${project.version}.jar"
	into        "${project.buildDir}/docker"
}

task copyDockerfile(type: Copy) {
	dependsOn   copyJar
	from        "${project.projectDir}/src/main/docker/Dockerfile"
	into        "${project.buildDir}/docker"
}

task buildImage(type: DockerBuildImage) {
	dependsOn   copyDockerfile
	inputDir = project.file("${project.buildDir}/docker")
	dockerFile = project.file("${project.buildDir}/docker/Dockerfile")
	files project.file("${project.buildDir}/docker")
	buildArgs = ['JAR_FILE' : "${jar.baseName}-${project.version}.jar"]
	images.add("aukhatov/srping-boot-auth-server:${project.version}")
}

docker {
	registryCredentials {
		url = 'https://index.docker.io/v1/'
		username = System.getenv('DOCKER_USER')
		password = System.getenv('DOCKER_PASSWORD')
	}
}

task pushImage(type: DockerPushImage) {
	dependsOn   buildImage
	imageName = "aukhatov/srping-boot-auth-server"
	tag = project.version
}
